plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'product'

// CVE-2024-49203 (SQL/HQL Injection, CVSS 9.8) 패치 반영
// 원본 com.querydsl은 5.1.0이 최신이므로, 패치된 OpenFeign 포크 사용
ext.querydslVersion = '5.6.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {


    implementation project(":common")
    implementation project(":member") // todo: 나중에 auth 분리되면 제거

    // spring boot
    implementation 'org.springframework.boot:spring-boot-h2console'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-kafka'

    // aop
    implementation 'org.springframework:spring-aop'
    implementation 'org.aspectj:aspectjweaver'

    // mapper
    implementation 'org.modelmapper:modelmapper:3.1.1'

    // mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Security + OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    testImplementation 'org.springframework.security:spring-security-test'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    // dotenv for loading .env file in tests
    testImplementation 'io.github.cdimascio:dotenv-java:3.0.0'
    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

    // MySQL Connector
    runtimeOnly 'com.mysql:mysql-connector-j'

    // ElasticSearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    // 통합 테스트 시 ON
    // testImplementation'org.springframework.boot:spring-boot-starter-data-elasticsearch-test'

    implementation 'io.hypersistence:hypersistence-tsid:2.1.1'

    // prometheus
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // querydsl (CVE-2024-49203 패치 버전: OpenFeign 포크 5.6.1)
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation("org.springframework.boot:spring-boot-starter-kafka-test")
}

bootJar {
    enabled = true
}

jar {
    enabled = true
}

tasks.named('test') {
    useJUnitPlatform {
        // 기본적으로 ignore 태그는 제외
        excludeTags 'ignore'

        // 필요하면 시스템 프로퍼티로 추가 제외
        def excluded = System.getProperty('junit.excludeTags')
        if (excluded) {
            excludeTags excluded.split(',')   // 예: "ignore,concurrency"
        }
    }

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams = false

        afterSuite { desc, result ->
            if (!desc.parent) { // 전체 실행(Root Suite) 결과인 경우만 출력
                println "\n---------------------------------------------------------"
                println "Test Result: ${result.resultType}"
                println "Summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
                println "---------------------------------------------------------\n"
            }
        }
    }

    // 테스트 실패 시 빌드 실패 처리
    ignoreFailures = false

    // Load .env file for tests
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            if (line && !line.startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)
                environment(key.trim(), value.trim().replaceAll(/^["']|["']$/, ''))
            }
        }
    }
}

tasks.withType(Test).configureEach {
    systemProperty "file.encoding", "UTF-8"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


tasks.named('check') {
    dependsOn 'spotlessCheck', 'checkstyleMain', 'checkstyleTest'
}

// TODO 추후 suppressions 파일로 분리
checkstyleMain.source = fileTree('src/main/java')

tasks.register("prepareKotlinBuildScriptModel"){}
