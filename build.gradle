plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id "com.diffplug.spotless" version "8.1.0"
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'back'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-h2console'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'

    // aop
    implementation 'org.springframework:spring-aop'
    implementation 'org.aspectj:aspectjweaver'

    // mapper
    implementation 'org.modelmapper:modelmapper:3.1.1'

    // mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Security + OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    testImplementation 'org.springframework.security:spring-security-test'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // dotenv for loading .env file in tests
    testImplementation 'io.github.cdimascio:dotenv-java:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    // MySQL Connector
    runtimeOnly 'com.mysql:mysql-connector-j'

    // AWS SDK
    implementation 'software.amazon.awssdk:s3:2.25.0'

    // ElasticSearch
    implementation "org.springframework.boot:spring-boot-starter-data-elasticsearch"

    implementation 'io.hypersistence:hypersistence-tsid:2.1.1'

    // prometheus
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
}

jar {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform(
            excludeTags: ['ignore']
    )

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams = false

        afterSuite { desc, result ->
            if (!desc.parent) { // 전체 실행(Root Suite) 결과인 경우만 출력
                println "\n---------------------------------------------------------"
                println "Test Result: ${result.resultType}"
                println "Summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
                println "---------------------------------------------------------\n"
            }
        }
    }

    // 테스트 실패 시 빌드 실패 처리
    ignoreFailures = false
    
    // Load .env file for tests
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            if (line && !line.startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)
                environment(key.trim(), value.trim().replaceAll(/^["']|["']$/, ''))
            }
        }
    }
}

spotless {
    java {
        googleJavaFormat("1.33.0")
        // Spotless와 Checkstyle 충돌나는 파일 제외
        targetExclude("**/OAuthUserInfo.java", "**/MemberSellerController.java")
        // diff 안정화를 위해 하기 옵션 추가
        trimTrailingWhitespace() // 줄 끝 공백 제거
        endWithNewline() // 파일 끝에 newline 강제
    }
}

checkstyle {
    toolVersion = '13.0.0'
    maxWarnings = 0 // 경고가 0개 이상이라면 빌드 시에 에러 발생
    configFile = file("config/checkstyle/google_checks.xml") // rule 적용
    ignoreFailures = false // 위반 시 실패(강제)
}

tasks.withType(Checkstyle).configureEach {
    // 리포트 출력
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.named('check') {
    dependsOn 'spotlessCheck', 'checkstyleMain', 'checkstyleTest'
}

// TODO 추후 suppressions 파일로 분리
checkstyleMain.source = fileTree('src/main/java')