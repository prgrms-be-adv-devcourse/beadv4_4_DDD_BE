plugins {
    id 'java'
    id 'checkstyle'
    id "com.diffplug.spotless" version "8.1.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.diffplug.spotless'

    group = 'com'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    checkstyle {
        toolVersion = '13.0.0'
        maxWarnings = 0 // 경고가 0개 이상이라면 빌드 시에 에러 발생
        configFile = rootProject.file("config/checkstyle/google_checks.xml") // rule 적용
        ignoreFailures = false // 위반 시 실패(강제)
        configProperties = [
                'charset': 'UTF-8'
        ]
    }
    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    spotless {
        java {
            googleJavaFormat("1.33.0")
            // Spotless와 Checkstyle 충돌나는 파일 제외
            targetExclude(
                    "**/OAuthUserInfo.java",
                    "**/MemberSellerController.java",
                    "**/generated/querydsl/**",
            )
            // diff 안정화를 위해 하기 옵션 추가
            trimTrailingWhitespace() // 줄 끝 공백 제거
            endWithNewline() // 파일 끝에 newline 강제
        }
    }

}

